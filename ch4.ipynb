{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 4.1 The Metacircular Evaluator"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "(define (eval exp env)\n",
    "  (cond ((self-evaluating? exp)\n",
    "         exp)\n",
    "        ((variable? exp)\n",
    "         (lookup-variable-value exp env))\n",
    "        ((quoted? exp)\n",
    "         (text-of-quotation exp))\n",
    "        ;; assignment/definition recursively call eval to comput\n",
    "        ;; new value associated with a variable\n",
    "        ((assignment? exp)\n",
    "         (eval-assignment exp env))\n",
    "        ((definition? exp)\n",
    "         (eval-definition exp env))\n",
    "        ((if? exp)\n",
    "         (eval-if exp env))\n",
    "        ;; transform into applicable procedure within environment\n",
    "        ((lambda? exp)\n",
    "         (eval-lambda exp env))\n",
    "        ;; evaluate sequence of expressions in order\n",
    "        ((begin? exp)\n",
    "         (eval-begin exp env))\n",
    "        ;; case analysis -> nest of if expressions which are then evaluated\n",
    "        ((cond? exp)\n",
    "         (eval-cond exp env))\n",
    "        ((application? exp)\n",
    "         (apply (eval (operator exp) env)\n",
    "                (list-of-values\n",
    "                 (operands exp)\n",
    "                 env)))\n",
    "        (else\n",
    "         (error \"Unkown expression\n",
    "                type: EVAL\" exp))))\n",
    "\n",
    "(define (apply procedure arguments)\n",
    "  (cond ((primitive-procedure? procedure)\n",
    "         (apply-primitive-procedure\n",
    "          procedure\n",
    "          arguments))\n",
    "        ;; sequentially eval each expression in procedure body\n",
    "        ;; extend base environment to include new frame binding\n",
    "        ;; params of procedure to the arguments it is applied to\n",
    "        ((compound-procedure? procedure)\n",
    "         (eval-sequence\n",
    "          (procedure-body procedure)\n",
    "          (extend-environment\n",
    "           (procedure-parameters\n",
    "            procedure)\n",
    "           arguments\n",
    "           (procedure-environment\n",
    "            procedure))))\n",
    "        (else\n",
    "         (error \"Unkown procedure\n",
    "                type: APPLY\"\n",
    "                procedure))))\n",
    "\n",
    ";; produce list of arguments to which a procedure is to be applied\n",
    ";; evaluate each operand -> return list of results\n",
    "(define (list-of-values exps env)\n",
    "  (if (no-operands? exps)\n",
    "      '()\n",
    "      (cons (eval (first-operand exps) env)\n",
    "            (list-of-values\n",
    "             (rest-operands exps)\n",
    "             env))))\n",
    "\n",
    "(define (eval-if exp env)\n",
    "  (if (true? (eval (if-predicate exp) env))\n",
    "      (eval (if-consequent exp) env)\n",
    "      (eval (if-alternative exp) env)))\n",
    "\n",
    ";; eval a sequence of expressions in order\n",
    ";; return value of final expression\n",
    "(define (eval-sequence exps env)\n",
    "  (cond ((last-exp? exps)\n",
    "         (eval (first-exp exps) env))\n",
    "        (else\n",
    "         (eval (first-exp exps) env)\n",
    "         (eval-sequence (rest-exps exps)\n",
    "                        env))))\n",
    "\n",
    ";; assign value to variable\n",
    "(define (eval-assignment exp env)\n",
    "  (set-variable-value!\n",
    "   (assignment-variable exp)\n",
    "   (eval (assignment-value exp) env)\n",
    "   env)\n",
    "  'ok)\n",
    "\n",
    ";; define a variable\n",
    "(define (eval-definition exp env)\n",
    "  (define-variable!\n",
    "   (definition-variable exp)\n",
    "   (eval (definition-value exp) env)\n",
    "   env)\n",
    "  'ok)\n",
    "\n",
    "(define (eval-begin exp env)\n",
    "  (eval-sequence\n",
    "   (begin-actions exp)\n",
    "   env))\n",
    "\n",
    "(define (eval-cond exp env)\n",
    "  (eval (cond->if exp) env))\n",
    "\n",
    "(define (eval-lambda exp env)\n",
    "  (make-procedure\n",
    "   (lambda-parameters exp)\n",
    "   (lambda-body exp)\n",
    "   env))\n",
    "\n",
    "\n",
    ";; expressions\n",
    "\n",
    "(define (self-evaluating? exp)\n",
    "  (cond ((number? exp) #t)\n",
    "        ((string? exp) #t)\n",
    "        (else #f)))\n",
    "\n",
    "(define (variable? exp) (symbol? exp))\n",
    "\n",
    ";; quotations: (quote <text-of-quotation>)\n",
    "(define (quoted? exp)\n",
    "  (tagged-list? exp 'quote))\n",
    "(define (text-of-quotation exp)\n",
    "  (cadr exp))\n",
    "\n",
    "(define (tagged-list? exp tag)\n",
    "  (if (pair? exp)\n",
    "      (eq? (car exp) tag)\n",
    "      false))\n",
    "\n",
    ";; assignments: (set! <var> <value>)\n",
    "(define (assignment? exp)\n",
    "  (tagged-list? exp 'set!))\n",
    "(define (assignment-variable exp)\n",
    "  (cadr exp))\n",
    "(define (assignment-value exp) (caddr exp))\n",
    "\n",
    ";; definitions: (define <var> <value>)\n",
    ";; OR (define (<var> <param-1> ... <param-n>) <body>)\n",
    "(define (definition? exp)\n",
    "  (tagged-list? exp 'define))\n",
    "(define (definition-variable exp)\n",
    "  (if (symbol? (cadr exp))\n",
    "      (cadr exp)\n",
    "      (caadr exp)))\n",
    "(define (definition-value exp)\n",
    "  (if (symbol? (cadr exp))\n",
    "      (caddr exp)\n",
    "      (make-lambda\n",
    "       (cdadr exp) ; formal parameters\n",
    "       (cddr exp)))) ; body\n",
    "\n",
    ";; lambda expressions: (lambda (<param-1> ... <param-n>) <body>)\n",
    "(define (lambda? exp)\n",
    "  (tagged-list? exp 'lambda))\n",
    "(define (lambda-parameters exp) (cadr exp))\n",
    "(define (lambda-body exp) (cddr exp))\n",
    "(define (make-lambda parameters body)\n",
    "  (cons 'lambda (cons parameters body)))\n",
    "\n",
    ";; conditionals: (if <predicate> <consequent> <alternative>?)\n",
    ";; alternative is optional -> default to false\n",
    "(define (if? exp) (tagged-list? exp 'if))\n",
    "(define (if-predicate exp) (cadr exp))\n",
    "(define (if-consequent exp) (caddr exp))\n",
    "(define (if-alternative exp)\n",
    "  (if (not (null? (cdddr exp)))\n",
    "      (cadddr exp)\n",
    "      'false)) ;; default to false if no alternative\n",
    "(define (make-if predicate\n",
    "                 consequent\n",
    "                 alternative)\n",
    "  (list 'if\n",
    "        predicate\n",
    "        consequent\n",
    "        alternative))\n",
    "\n",
    ";; begin: (begin <exp-1> ... <exp-n>)\n",
    ";; package a sequence of expressions into a single expression\n",
    "(define (begin? exp)\n",
    "  (tagged-list? exp 'begin))\n",
    "(define (begin-actions exp) (cdr exp))\n",
    "(define (last-exp? seq) (null? (cdr seq)))\n",
    "(define (first-exp seq) (car seq))\n",
    "(define (rest-exps seq) (cdr seq))\n",
    "(define (sequence->exp seq)\n",
    "  (cond ((null? seq) seq)\n",
    "        ((last-exp? seq) (first-exp seq))\n",
    "        (else (make-begin seq))))\n",
    "(define (make-begin seq) (cons 'begin seq))\n",
    "\n",
    ";; procedure application: any compound expression not defined as an expression type above\n",
    ";; (<operator> <operand-1> ... <operand-n>)\n",
    "(define (application? exp) (pair? exp))\n",
    "(define (operator exp) (car exp))\n",
    "(define (operands exp) (cdr exp))\n",
    "(define (no-operands? ops) (null? ops))\n",
    "(define (first-operand ops) (car ops))\n",
    "(define (rest-operands ops) (cdr ops))\n",
    "\n",
    ";; cond: (cond ((<predicate-1> <result-1> ... <predicate-n> <result-1n) (else <result>))\n",
    ";; implemented as nested if expressions\n",
    "(define (cond? exp)\n",
    "  (tagged-list? exp 'cond))\n",
    "(define (cond-clauses exp) (cdr exp))\n",
    "(define (cond-else-clause? clause)\n",
    "  (eq? (cond-predicate clause) 'else))\n",
    "(define (cond-predicate clause)\n",
    "  (car clause))\n",
    "(define (cond-actions clause)\n",
    "  (cdr clause))\n",
    "(define (cond->if exp)\n",
    "  (expand-clauses (cond-clauses exp)))\n",
    "(define (expand-clauses clauses)\n",
    "  (if (null? clauses)\n",
    "      'false ;; no else clause\n",
    "      (let ((first (car clauses))\n",
    "            (rest (cdr clauses)))\n",
    "        (if (cond-else-clause? first)\n",
    "            (if (null? rest)\n",
    "                (sequence->exp\n",
    "                 (cond-actions first))\n",
    "                (error \"ELSE clause is not last: COND->IF\"\n",
    "                       clauses))\n",
    "            (make-if (cond-predicate first)\n",
    "                     (sequence->exp\n",
    "                      (cond-actions first))\n",
    "                     (expand-clauses\n",
    "                      rest))))))"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 4.1\n",
    "Nested `let`s will force the operands to be evaluated in the desired order. The `let` must be nested as multiple assignments in a single let will use the underlying Scheme order of evaluation:\n",
    "```scheme\n",
    "(let ((foo exp1) ((bar exp2)))\n",
    "  ...)\n",
    "```\n",
    "Is equivalent to:\n",
    "```scheme\n",
    "((lambda (foo bar)...) exp1 exp2)\n",
    "```"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [],
   "source": [
    "(define (list-of-values-lr exps env)\n",
    "  (if (no-operands? exps)\n",
    "      '()\n",
    "      (let ((left (eval (first-operand exps) env)))\n",
    "        (let ((right (list-of-values-lr (rest-operands exps) env)))\n",
    "          (cons left right)))))\n",
    "\n",
    "(define (list-of-values-rl exps env)\n",
    "  (if (no-operands? exps)\n",
    "      '()\n",
    "      (let ((right (list-of-values-lr (rest-operands exps) env)))\n",
    "        (let ((left (eval (first-operand exps) env)))\n",
    "          (cons right left)))))"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 4.2\n",
    "1. Moving the clause for procedure applications to before the clause for assignments produces the following for `(define x 3)`:\n",
    "```scheme\n",
    "(eval '(define x 3) env)\n",
    "(application? '(define x 3)) => true\n",
    "(apply (eval 'define env) (list-of-values ('x 3) env))\n",
    "(eval 'define env)\n",
    "(variable? 'define) => true\n",
    "(lookup-variable-value 'define env) ERROR\n",
    "```\n",
    "2. Changing procedure applcations to start with `call` requires changing the `application?` predicate and `operator`/`operand` selectors:\n",
    "```scheme\n",
    "(define (application? exp)\n",
    "  (tagged-list? exp 'call))\n",
    "(define (operator exp) (cadr exp))\n",
    "(define (operand exp) (cddr exp))\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 4.3"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [],
   "source": [
    "(define (eval-data-directed exp env)\n",
    "  (cond ((self-evaluating? exp)\n",
    "         exp)\n",
    "        ((variable? exp)\n",
    "         (lookup-variable-value exp env))\n",
    "        ((get (car exp))\n",
    "         ((get (car exp)) exp env))\n",
    "        ((application? exp)\n",
    "         (apply (eval (operator exp) env)\n",
    "                (list-of-values\n",
    "                 (operands exp)\n",
    "                 env)))\n",
    "        (else\n",
    "         (error \"Unkown expression\n",
    "                type: EVAL\" exp))))\n",
    "\n",
    "(define (install-eval-data-directed)\n",
    "  (put 'quote text-of-quotation)\n",
    "  (put 'set! eval-assignment) \n",
    "  (put 'define eval-definition)\n",
    "  (put 'if eval-if)\n",
    "  (put 'lambda eval-lambda)\n",
    "  (put 'begin eval-begin)\n",
    "  (put 'cond eval-cond)\n",
    "  'done)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 4.4\n",
    "#### `and`\n",
    "- Expressions evaluated left->right\n",
    "- Any expression evaluates to false -> return false\n",
    "    - Remaining expressions **not** evaluated\n",
    "- All expressions evaluate to true -> return value of final expression\n",
    "- No expressions -> return true\n",
    "\n",
    "#### `or`\n",
    "- Expressions evaluated left->right\n",
    "- Any expression evalues to true -> return that value\n",
    "    - Remaining expressions **not** evaluated\n",
    "- All expressions evaluate to false -> return false\n",
    "- No expressions -> return false"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [],
   "source": [
    ";; extend to include boolean values\n",
    "(define (self-evaluating? exp)\n",
    "  (or (number? exp)\n",
    "      (string? exp)\n",
    "      (boolean? exp)))\n",
    "\n",
    "(define (and? exp)\n",
    "  (tagged-list? exp 'and))\n",
    "(define (or? exp)\n",
    "  (tagged-list? exp 'or))\n",
    "(define (boolean-exps exp)\n",
    "  (cdr exp))\n",
    "\n",
    "(define (eval-and exp env)\n",
    "  (define (eval-exps exp)\n",
    "    (cond ((last-exp? exp)\n",
    "           (eval (first-exp exps) env))\n",
    "          ((eval (first-exp exps) env)\n",
    "           (eval-exps (rest-exps exp)))\n",
    "          (else #f)))\n",
    "  (eval-exps (boolean-exps exp)))\n",
    "\n",
    "(define (eval-or exp env)\n",
    "  (define (eval-exps exp)\n",
    "    (cond ((last-exp? exp)\n",
    "           (eval (first-exp exps) env))\n",
    "          ((eval (first-exp exps) env) #t)\n",
    "          (else (eval-exps (rest-exps exp)))))\n",
    "  (eval-exps (boolean-exps exp)))\n",
    "\n",
    "\n",
    ";; OR with derived expressions using nested if\n",
    "(define (eval-and exp env)\n",
    "  (eval (and->if (boolean-exps exp) env) env))\n",
    "(define (eval-or exp env)\n",
    "  (eval (or->if (boolean-exps exp) env) env))\n",
    "\n",
    "(define (and->if exp env)\n",
    "  (cond ((last-exp? exp)\n",
    "         (eval (first-exp exp) env))\n",
    "        (else (make-if\n",
    "               (first-exp exp)\n",
    "               (and->if (rest-exps exp) env)\n",
    "               #f))))\n",
    "\n",
    "(define (or->if exp env)\n",
    "  (cond ((last-exp? exp)\n",
    "         (eval (first-exp exp) env))\n",
    "        (else (make-if\n",
    "               (first-exp exp)\n",
    "               #t\n",
    "               (or->if (rest-exps exp) env)))))\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 4.5\n",
    "Extend `cond` to support `(<test> => <recipient>)` syntax."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [],
   "source": [
    "(define (cond-arrow-form? clause)\n",
    "  (eq? (cadr clause) '=>))\n",
    "(define (cond-recipient clause)\n",
    "  (caddr clause))\n",
    "\n",
    ";; ((<predicate-clauses>) (<result-clauses>)) -> (<result-clauses>)\n",
    ";; ((<test>) => <recipient>) -> (<recipient> (<test>))\n",
    "(define (cond-consequent clause predicate)\n",
    "  (if (cond-arrow-form? clause)\n",
    "      (list (cond-recipient clause) predicate)\n",
    "      (sequence->exp (cond-actions clause))))\n",
    "\n",
    "(define (expand-clauses clauses)\n",
    "  (if (null? clauses)\n",
    "      'false ;; no else clause\n",
    "      (let ((first (car clauses))\n",
    "            (rest (cdr clauses)))\n",
    "        (if (cond-else-clause? first)\n",
    "            (if (null? rest)\n",
    "                (sequence->exp\n",
    "                 (cond-actions first))\n",
    "                (error \"ELSE clause is not last: COND->IF\"\n",
    "                       clauses))\n",
    "            (let ((predicate (cond-predicate first)))\n",
    "              (make-if predicate\n",
    "                       (cond-consequent first predicate)\n",
    "                       (expand-clauses rest)))))))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Calysto Scheme 3",
   "language": "scheme",
   "name": "calysto_scheme"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "scheme"
   },
   "mimetype": "text/x-scheme",
   "name": "scheme",
   "pygments_lexer": "scheme"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
