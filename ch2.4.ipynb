{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 2.4\n",
    "Separate notebook as `ch2.ipynb` got large and unwieldly/slow."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Complex-Number System\n",
    "\n",
    "The complex number $z=x+iy$ (where $i^2=-1$) can be thought of as the point in the plane whose *real* coordinate is $x$ and whose *imaginary* coordinate is $y$.\n",
    "\n",
    "Can have 2 representations:\n",
    "- Reactangular form\n",
    "    - Real part and imaginary part\n",
    "- Polar form\n",
    "    - Magnitude and angle\n",
    "    \n",
    "![](images/complex-numbers.png)\n",
    "\n",
    "#### Addition \n",
    "Of two complex numbers $z_1,z_2$ reduces to addition of coordinates - using rectangular form:\n",
    "\n",
    "$RealPart(z_1+z_2)=RealPart(z_1)+RealPart(z_2)$\n",
    "\n",
    "$ImaginaryPart(z_1+z_2)=ImaginaryPart(z_1)+ImaginaryPart(z_2)$\n",
    "\n",
    "#### Product \n",
    "Of two complex numbers $z_1,z_2$ is the vector obtained by stretching one complex number by the length of the other and then rotating it throught the angle of the other - using polar form:\n",
    "\n",
    "$Magnitude(z_1\\cdot z_2) = Magnitude(z_1)\\cdot Magnitude(z_2)$\n",
    "\n",
    "$Angle(z_1\\cdot z_2) = Angle(z_1)+Magnitude(z_2)$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [],
   "source": [
    ";; ASSUME put and get procedures are implemented in the language\n",
    "(define (install-rectangular-package)  \n",
    "  ;; internal procedures\n",
    "  (define (real-part z) (car z))\n",
    "  (define (imag-part z) (cdr z))\n",
    "  (define (make-from-real-imag x y)\n",
    "    (cons x y))\n",
    "  (define (magnitude z)\n",
    "    (sqrt (+ (square (real-part z))\n",
    "             (square (imag-part z)))))\n",
    "  (define (angle z)\n",
    "    (atan (imag-part z) (real-part z)))\n",
    "  (define (make-from-mag-ang r a)\n",
    "    (cons (* r (cos a)) (* r (sin a))))\n",
    "  ;; interface to the rest of the system\n",
    "  (define (tag x)\n",
    "    (attach-tag 'rectangular x))\n",
    "  (put 'real-part '(rectangular) real-part)\n",
    "  (put 'imag-part '(rectangular) imag-part)\n",
    "  (put 'magnitude '(rectangular) magnitude)\n",
    "  (put 'angle '(rectangular) angle)\n",
    "  (put 'make-from-real-imag 'rectangular\n",
    "       (lambda (x y)\n",
    "         (tag (make-from-real-imag x y))))\n",
    "  (put 'make-from-mag-ang 'rectangular\n",
    "       (lambda (r a)\n",
    "         (tag (make-from-mag-ang r a))))\n",
    "  'done)\n",
    "\n",
    "(define (install-polar-package)\n",
    "  ;; internal procedures\n",
    "  (define (magnitude z) (car z))\n",
    "  (define (angle z) (cdr z))\n",
    "  (define (make-from-mag-ang r a) (cons r a))\n",
    "  (define (real-part z)\n",
    "    (* (magnitude z) (cos (angle z))))\n",
    "  (define (imag-part z)\n",
    "    (* (magnitude z) (sin (angle z))))\n",
    "  (define (make-from-real-imag x y)\n",
    "    (cons (sqrt (+ (square x) (square y)))\n",
    "          (atan y x)))\n",
    "  ;; interface to the rest of the system\n",
    "  (define (tag x) (attach-tag 'polar x))\n",
    "  (put 'real-part '(polar) real-part)\n",
    "  (put 'imag-part '(polar) imag-part)\n",
    "  (put 'magnitude '(polar) magnitude)\n",
    "  (put 'angle '(polar) angle)\n",
    "  (put 'make-from-real-imag 'polar\n",
    "       (lambda (x y)\n",
    "         (tag (make-from-real-imag x y))))\n",
    "  (put 'make-from-mag-ang 'polar\n",
    "       (lambda (r a)\n",
    "         (tag (make-from-mag-ang r a))))\n",
    "  'done)\n",
    "\n",
    "(define (apply-generic op . args)\n",
    "  (let ((type-tags (map type-tag args)))\n",
    "    (let ((proc (get op type-tags)))\n",
    "      (if proc\n",
    "          (apply proc (map contents args))\n",
    "          (error\n",
    "           \"No method for these types:\n",
    "           APPLY-GENERIC\"\n",
    "           (list op type-tags))))))\n",
    "\n",
    ";; generic selectors\n",
    "(define (real-part z)\n",
    "  (apply-generic 'real-part z))\n",
    "(define (imag-part z)\n",
    "  (apply-generic 'imag-part z))\n",
    "(define (magnitude z)\n",
    "  (apply-generic 'magnitude z))\n",
    "(define (angle z)\n",
    "  (apply-generic 'angle z))\n",
    "\n",
    ";; generic constructors\n",
    "(define (make-from-real-imag x y)\n",
    "  ((get 'make-from-real-imag\n",
    "        'rectangular)\n",
    "   x y))\n",
    "(define (make-from-mag-ang r a)\n",
    "  ((get 'make-from-mag-ang\n",
    "        'polar)\n",
    "   r a))"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 2.73"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [],
   "source": [
    ";; original deriv implementation from 2.3.2 for reference\n",
    ";; other procedures used defined in ch2.ipynb\n",
    "(define (deriv exp var)\n",
    "  (cond ((number? exp) 0)\n",
    "        ((variable? exp)\n",
    "         (if (same-variable? exp var) 1 0))\n",
    "        ((sum? exp)\n",
    "         (make-sum (deriv (addend exp) var)\n",
    "                   (deriv (augend exp) var)))\n",
    "        ((product? exp)\n",
    "         (make-sum\n",
    "          (make-product\n",
    "           (multiplier exp)\n",
    "           (deriv (multiplicand exp) var))\n",
    "          (make-product\n",
    "           (deriv (multiplier exp) var)\n",
    "           (multiplicand exp))))\n",
    "        ;; more derivation rules here\n",
    "        (else (error \"unknown expression type:\n",
    "                     DERV\" exp))))\n",
    "\n",
    ";; data-directed style deriv\n",
    "(define (deriv exp var)\n",
    "  (cond ((number? exp) 0)\n",
    "        ((variable? exp)\n",
    "         (if (same-variable? exp var)\n",
    "             1\n",
    "             0))\n",
    "        (else ((get 'deriv (operator exp))\n",
    "               (operands exp)\n",
    "               var))))\n",
    "\n",
    "(define (operator exp) (car exp))\n",
    "(define (operands exp) (cdr exp))"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The logic for implementing different derivation rules based on the type of expression passed to `deriv` has been move out of the body of `deriv`- with a separate procedure defined for each type of expression. These are installed in the dispatch table with the *operator* of the expression used as their type-tag. `deriv` then retrieves the appropriate procedure based on the operator of `exp` and applies it to the operands of `exp` and the given variable.\n",
    "\n",
    "`number?` and `variable?` can't be assimilated into the data-directed dispatch as they must be able to act on single values which do not contain operators (only a number or a symbol). This means that a type-tag can't be assigned to them to retrieve the procedure from the dispatch table using `get`."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "metadata": {},
   "outputs": [],
   "source": [
    "(define (deriv-sum exp var)\n",
    "  (make-sum (deriv (addend exp) var)\n",
    "            (deriv (augend exp) var)))\n",
    "\n",
    "(define (deriv-product exp var)\n",
    "  (make-sum\n",
    "   (make-product\n",
    "    (multiplier exp)\n",
    "    (deriv (multiplicand exp) var))\n",
    "   (make-product\n",
    "    (deriv (multiplier exp) var)\n",
    "    (multiplicand exp))))\n",
    "\n",
    "(define (install-deriv-procs)\n",
    "  (put 'deriv '+ deriv-sum)\n",
    "  (put 'deriv '* deriv-product)\n",
    "  'done)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "(define (deriv-exponentiation exp var)\n",
    "  (make-product\n",
    "   (make-product (exponent exp)\n",
    "                 (make-exponentiation (base exp)\n",
    "                                      (- (exponent exp) 1)))\n",
    "   (deriv (base exp) var)))\n",
    "\n",
    "(define (install-deriv-exponentiation)\n",
    "  (put 'deriv '** deriv-exponentiation)\n",
    "  'done)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Only changes are to swap the order of the procedure and type-tag/operator in the installation procedures:\n",
    "```scheme\n",
    "(put '+ 'deriv deriv-sum)\n",
    "(put '* 'deriv deriv-product)\n",
    "(put '** 'deriv deriv-exponentiation)\n",
    "```"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Calysto Scheme 3",
   "language": "scheme",
   "name": "calysto_scheme"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "scheme"
   },
   "mimetype": "text/x-scheme",
   "name": "scheme",
   "pygments_lexer": "scheme"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
